import java.time.LocalDate

plugins {
    id 'eclipse'
    id 'maven'
    id 'com.github.hierynomus.license' version '0.12.1'
    id 'com.diffplug.gradle.spotless' version '1.3.2'
    id 'org.spongepowered.plugin' version '0.6'
    id 'com.qixalite.spongestart' version '1.4.6'
}

defaultTasks 'clean', 'build'

dependencies {
    compile 'org.spongepowered:spongeapi:' + project.spongeapi
}

spotless {
    java {
        importOrderFile 'gradle/spotless/order.importorder'
        eclipseFormatFile 'gradle/spotless/formatter.xml'
        custom 'Long literal fix', { it.replaceAll('([0-9_]+) [Ll]', '$1L') }
    }
}

license {
    ext {
        name = project.name
        organization = project.organization
        url = project.url
        inceptionYear = '2016'
        currentYear = LocalDate.now().getYear();
    }

    header = file('gradle/HEADER.txt')
    include '**/*.java'
}

spongestart {
    eula true
}
